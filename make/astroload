#---do not execute-source this in csh
# add the following alias to your .cshrc file:
#
# alias astroload 'set _astroload=(\!*); source /usr/local/astro/make/astroload'
# 
# in order to more easily load packages on the fly
#

if ($?astroload == 0) set astroload=()


foreach package ($_astroload)
  if (-e /usr/local/astro/status/$package) then
    if ($?prompt) echo Loading $package version `cat /usr/local/astro/status/$package`
    source /usr/local/astro/rc/$package.csh
    set astroload=($astroload $package)
  else
    echo Package $package does not exist
  endif
end

if ($#_astroload == 0) then
  echo The following astromake packages have been loaded in this shell with astroload:
  echo $astroload
endif

unset _astroload
